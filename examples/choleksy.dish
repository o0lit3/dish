// https://en.wikipedia.org/wiki/Cholesky_decomposition

cholesky = :a(
  out: [[0] * #a] * #a

  (0..#a-1).each:i(
    (0..i).each:j(
      sum: (j > 0).switch[(0..j-1).map:k(out.i.k * out.j.k).sum, 0]

      out.i.j = (i == j).switch[
        ((a.i.i - sum)**0.5)
        (1 / out.j.j * (a.i.j - sum))
      ]
    )
  )

  out.map:row(
    row.map:col(
      col.format("%8.5f")
    ).join(" ")
  ).join("\n")
)

[
  [[
    [25, 15, -5]
    [15, 18,  0]
    [-5,  0, 11]
  ]].cholesky

  [[
    [18, 22,  54,  42]
    [22, 70,  86,  62]
    [54, 86, 174, 134]
    [42, 62, 134, 106]
  ]].cholesky
].join("\n\n")
