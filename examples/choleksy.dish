## https://en.wikipedia.org/wiki/Cholesky_decomposition

cholesky = :a(
  out: [[0] * #a] * #a

  (0..#a - 1).each:row(
    (0..row).each:col(
      x: (0..col).reduce:x:k(
        (x ?= a.row.col) - out.row.k * out.col.k
      )

      out.row.col = (row == col).switch[
        x.sqrt
        x / out.col.col
      ]
    )
  )

  out.map:row(
    row.map:col(
      col.fmt("%8.5f")
    ).join(' ')
  ).join
)

[
  [
    [25, 15, -5]
    [15, 18,  0]
    [-5,  0, 11]
  ].cholesky

  [
    [18, 22,  54,  42]
    [22, 70,  86,  62]
    [54, 86, 174, 134]
    [42, 62, 134, 106]
  ].cholesky
].join("\n\n")
